# Note you need gnuplot 4.4 for the pdfcairo terminal.

set terminal pdfcairo font "Helvetica,22" linewidth 4 rounded

# Line style for axes
set style line 80 lt rgb "#808080"

# Line style for grid
set style line 81 lt 0  # dashed
set style line 81 lt rgb "#808080"  # grey

#set key at 2250,230
set key top left invert reverse Left opaque samplen 1 spacing 1.8 width -8 height 1 #box 7

set grid back linestyle 81
set border 3 back linestyle 80 # Remove border on top and right.  These
             # borders are useless and make it harder
             # to see plotted lines near the border.
    # Also, put it in grey; no need for so much emphasis on a border.
set xtics nomirror
set ytics nomirror

#set log x
#set mxtics 10    # Makes logscale look good.

# Line styles: try to pick pleasing colors, rather
# than strictly primary colors or hard-to-see colors
# like gnuplot's default yellow.  Make the lines thick
# so they're easy to see in small plots in papers.
set style line 3 lt rgb "#00A000" lw 2 pt 6
set style line 2 lt rgb "#A00000" lw 2 pt 1
set style line 1 lt rgb "#5060D0" lw 2 pt 2
set style line 4 lt rgb "#F25900" lw 2 pt 9



set ylabel "Time in Seconds"
set xlabel "Number of Switches"

set yrange [0:]

# Note that we're leaving out output, title, and plot
set output "scaling.pdf"
#set title "STS Scaling on FatTree Networks"
plot 'scaling.dat' using 1:2 title "OpenFlow Handshakes" w points ls 1, \
     'scaling.dat' using 1:3 title "+ 5% Link Failures" w points ls 2, \
     'scaling.dat' using 1:4 title "Total Including Initialization" w points ls 3
